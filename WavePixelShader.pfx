
sampler2D texture0				: register(s0);
sampler2D normalMap				: register(s1);
float4 ambientColor				: register(c0);
float4 directionalLightColor	: register(c1);
float4 lightAmbDiffSpecPower	: register(c2);
float4 simParams				: register(c3);

struct PS_INPUT
{
    float2 texCoords : TEXCOORD0;
    float3 light	 : TEXCOORD1;
    float3 normal	 : TEXCOORD2;
	float3 view		 : TEXCOORD3;
    float4 diffuse   : COLOR0;
    float4 specular  : COLOR1;
};

// NOTE: all light calculations here are done in tangent space. WaveVertex shader transforms normal, light and view to tangent space
float4 main(PS_INPUT input) : COLOR0
{
	// Sample texture and normal map
	float4 texColor = float4(0.01,0.3,0.7,1); //tex2D(texture0, input.texCoords); // float4(0.01,0.3,0.7,1); - good color if we don't want to use texture
	float3 normal = normalize(tex2D(normalMap, input.texCoords).rgb*2.0f-1.0f);

    // Normalize all vectors since they may not be normalized after interpolation
    float3 lightDir = normalize(input.light);
    float3 viewDir = normalize(input.view); 
    
    // Calculate diffuse component
    float dotProdNormalLight = dot(normal, lightDir);
    float4 diffuseCoeff = saturate(dotProdNormalLight);

	// Calculate specular component
    float3 reflected = normalize(2 * dotProdNormalLight * normal - lightDir);
    float4 specularCoeff = pow(saturate(dot(reflected, viewDir)), lightAmbDiffSpecPower.w);
    
    // Combine all components
    float4 pixelColor = 
		(directionalLightColor * lightAmbDiffSpecPower.x * ambientColor + directionalLightColor * lightAmbDiffSpecPower.y * input.diffuse * diffuseCoeff) * texColor + 
		directionalLightColor * specularCoeff * input.specular;

	pixelColor.a = 0.9;

	return pixelColor;
}
